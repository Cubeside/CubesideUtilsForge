plugins {
//     id 'java'
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = '1.0-SNAPSHOT'
group = 'de.cubeside.cubesideutils' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'cubesideutilsforge'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.18.2'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.
}

repositories {
    mavenLocal()
    maven { url = 'https://www.iani.de/nexus/content/groups/public' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.2.14'

    compileOnly 'net.luckperms:api:5.4'
    api 'de.iani.cubeside:CubesideUtilsCore:1.16-SNAPSHOT'
    implementation 'com.mysql:mysql-connector-j:8.0.33'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "cubesideutilsforge",
                "Specification-Vendor"    : "Brokkonaut",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Brokkonaut",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')
// shadowJar.finalizedBy('reobfJar')
// publish.dependsOn('reobfJar')

reobf {
    shadowJar {}
}

shadowJar {
    // archiveBaseName.set('cubesideutilsforge')
    archiveClassifier.set('shaded')

    dependencies {
        include(dependency('de.iani.cubeside:CubesideUtilsCore'))
        include(dependency('com.mysql:mysql-connector-j'))
        exclude "META-INF/services/java.sql.Driver"
    }
    
    relocate 'com.mysql', 'de.iani.cubesideutils.forge.libs.mysql'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}